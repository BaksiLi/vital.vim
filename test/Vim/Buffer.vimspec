scriptencoding utf-8

let s:V = vital#vital#new()
let s:P = s:V.import('System.Filepath')
let s:G = s:V.import('Vim.Guard')

Describe Vim.Buffer
  Before all
    let Buffer = s:V.import('Vim.Buffer')
  End
  After all
    windo bwipeout!
  End
  Before
    windo bwipeout!
  End

  Describe .is_cmdwin()
    It detects if current window is cmdwin.
      " FIXME: CmdWin can not open from test environment
      Assert Falsy(Buffer.is_cmdwin())
    End
  End

  Describe .get_last_selected()
    Context for single byte characters
      Before
        execute printf('edit %s',
              \ s:P.realpath('test/_testdata/Vim/Buffer/single.txt')
              \)
      End

      It can get the last selected text without textlock error.
        normal! ggVGygg
        let text = Buffer.get_last_selected()
        Assert Equals(text, "foo\nbar\nbaz\n")

        normal! ggvjly
        let text = Buffer.get_last_selected()
        Assert Equals(text, "foo\nba")

        execute "normal! gg\<C-v>jly"
        let text = Buffer.get_last_selected()
        Assert Equals(text, "fo\nba")
      End

      It can apply to oneline
        normal! ggVygg
        let text = Buffer.get_last_selected()
        Assert Equals("foo\n", text)

        normal! ggvly
        let text = Buffer.get_last_selected()
        Assert Equals("fo", text)

        execute "normal! gg\<C-v>ly"
        let text = Buffer.get_last_selected()
        Assert Equals("fo", text)
      End

      It does not destroy unnamed register content
        normal! ggVGy
        let @" = "ajapa-"
        call Buffer.get_last_selected()
        Assert Equals("ajapa-", @")
      End
    End

    Context for wide characters
      Before
        execute printf('edit %s',
              \ s:P.realpath('test/_testdata/Vim/Buffer/wide.txt')
              \)
      End

      It can apply to wide characters string
        normal! ggVGygg
        let text = Buffer.get_last_selected()
        Assert Equals("あいうえお\nかきくけこ\nほげら\n", text)

        normal! ggvjly
        let text = Buffer.get_last_selected()
        Assert Equals("あいうえお\nかき", text)

        execute "normal! gg\<C-v>jly"
        let text = Buffer.get_last_selected()
        Assert Equals("あい\nかき", text)
      End
    End
    Context for mixed characters
      Before
        execute printf('edit %s',
              \ s:P.realpath('test/_testdata/Vim/Buffer/mixed.txt')
              \)
      End

      It can apply to mixed string
        normal! ggVGygg
        let text = Buffer.get_last_selected()
        Assert Equals("あiうeお\nkaきkuけko\nほgeら\n", text)

        normal! ggvjly
        let text = Buffer.get_last_selected()
        Assert Equals("あiうeお\nka", text)

        execute "normal! gg\<C-v>jly"
        let text = Buffer.get_last_selected()
        Assert Equals("あ\nka", text)
      End

      It can apply to mixed oneline string
        normal! ggVygg
        let text = Buffer.get_last_selected()
        Assert Equals("あiうeお\n", text)

        normal! ggvly
        let text = Buffer.get_last_selected()
        Assert Equals("あi", text)

        execute "normal! gg\<C-v>ly"
        let text = Buffer.get_last_selected()
        Assert Equals("あi", text)
      End
    End
  End

  Describe read_content()
    Before
      let guard = s:G.store(['&fileencodings', '&fileencoding'])
      " Note:
      " 'read_content' uses 'read' command thus fileencodings requires to be
      " correctly configured.
      set fileencodings=utf-8,euc-jp,sjis
      set fileencoding=utf-8
    End
    After
      call guard.restore()
    End

    Context for utf-8 contents
      It appends contents to the current buffer
        call setline(1, 'あいうえお')
        let contents = readfile(
              \ s:P.realpath('test/_testdata/Vim/Buffer/utf-8.txt')
              \)
        call Buffer.read_content(contents)
        Assert Equals(getline(1, '$'), [
              \ 'あいうえお',
              \ 'あいうえお',
              \])
        Assert Equals(&fileencoding, 'utf-8')
      End
    End
    Context for sjis contents
      It appends contents to the current buffer
        call setline(1, 'あいうえお')
        let contents = readfile(
              \ s:P.realpath('test/_testdata/Vim/Buffer/sjis.txt')
              \)
        call Buffer.read_content(contents)
        Assert Equals(getline(1, '$'), [
              \ 'あいうえお',
              \ 'あいうえお',
              \])
        Assert Equals(&fileencoding, 'utf-8')
      End
    End
    Context for euc-jp contents
      It appends contents to the current buffer
        call setline(1, 'あいうえお')
        let contents = readfile(
              \ s:P.realpath('test/_testdata/Vim/Buffer/euc-jp.txt')
              \)
        call Buffer.read_content(contents)
        Assert Equals(getline(1, '$'), [
              \ 'あいうえお',
              \ 'あいうえお',
              \])
        Assert Equals(&fileencoding, 'utf-8')
      End
    End
    It wipes out tempfile buffer
      let fname = tempname()
      call Buffer.read_content([''], {'tempfile': fname})
      Assert Equals(bufnr(fname), -1)
    End

    It append content after cursor
        call setline(1, ['aaaaa', 'bbbbb', 'ccccc'])
        call setpos('.', [0, 2, 1, 0])
        let contents = readfile(
              \ s:P.realpath('test/_testdata/Vim/Buffer/utf-8.txt')
              \)
        call Buffer.read_content(contents)
        Assert Equals(getline(1, '$'), [
              \ 'aaaaa',
              \ 'bbbbb',
              \ 'あいうえお',
              \ 'ccccc',
              \])
    End

    It append content after {options.line}
        call setline(1, ['aaaaa', 'bbbbb', 'ccccc'])
        let contents = readfile(
              \ s:P.realpath('test/_testdata/Vim/Buffer/utf-8.txt')
              \)
        call Buffer.read_content(contents, {
              \ 'line': 2,
              \})
        Assert Equals(getline(1, '$'), [
              \ 'aaaaa',
              \ 'bbbbb',
              \ 'あいうえお',
              \ 'ccccc',
              \])
    End

    It insert content before the first line when {options.line} is 0
        call setline(1, ['aaaaa', 'bbbbb', 'ccccc'])
        let contents = readfile(
              \ s:P.realpath('test/_testdata/Vim/Buffer/utf-8.txt')
              \)
        call Buffer.read_content(contents, {
              \ 'line': 0,
              \})
        Assert Equals(getline(1, '$'), [
              \ 'あいうえお',
              \ 'aaaaa',
              \ 'bbbbb',
              \ 'ccccc',
              \])
    End
  End

  Describe edit_content()
    Before
      let guard = s:G.store(['&fileencodings', '&fileencoding'])
      " Note:
      " 'edit_content' uses 'read' command thus fileencodings requires to be
      " correctly configured.
      set fileencodings=utf-8,euc-jp,sjis
      set fileencoding=utf-8
    End
    After
      call guard.restore()
    End

    Context for utf-8 contents
      It replaces contents to the current buffer
        call setline(1, 'かきくけこ')
        let contents = readfile(
              \ s:P.realpath('test/_testdata/Vim/Buffer/utf-8.txt')
              \)
        call Buffer.edit_content(contents)
        Assert Equals(getline(1, '$'), [
              \ 'あいうえお',
              \])
        Assert Equals(&fileencoding, 'utf-8')
      End
    End
    Context for sjis contents
      It replaces contents to the current buffer
        call setline(1, 'かきくけこ')
        let contents = readfile(
              \ s:P.realpath('test/_testdata/Vim/Buffer/sjis.txt')
              \)
        call Buffer.edit_content(contents)
        Assert Equals(getline(1, '$'), [
              \ 'あいうえお',
              \])
        Assert Equals(&fileencoding, 'sjis')
      End
    End
    Context for euc-jp contents
      It replaces contents to the current buffer
        call setline(1, 'かきくけこ')
        let contents = readfile(
              \ s:P.realpath('test/_testdata/Vim/Buffer/euc-jp.txt')
              \)
        call Buffer.edit_content(contents)
        Assert Equals(getline(1, '$'), [
              \ 'あいうえお',
              \])
        Assert Equals(&fileencoding, 'euc-jp')
      End
    End
  End

  Describe parse_cmdarg()
    It returns an empty dictionary for non cmdarg
      let ret = Buffer.parse_cmdarg('')
      Assert Equals(ret, {})
    End
    It parses "++enc=XXX" and return "encoding: XXX" dictionary
      let ret = Buffer.parse_cmdarg('++enc=utf-8')
      Assert Equals(ret, { 'encoding': 'utf-8' })
    End

    It parses "++ff=XXX" and return "fileformat: XXX" dictionary
      let ret = Buffer.parse_cmdarg('++ff=dos')
      Assert Equals(ret, { 'fileformat': 'dos' })
    End

    It parses "++bad=XXX" and return "bad: XXX" dictionary
      let ret = Buffer.parse_cmdarg('++bad=keep')
      Assert Equals(ret, { 'bad': 'keep' })
    End

    It parses "++bin" and return "binary: 1" dictionary
      let ret = Buffer.parse_cmdarg('++binary')
      Assert Equals(ret, { 'binary': 1 })
    End

    It parses "++nobin" and return "nobinary: 1" dictionary
      let ret = Buffer.parse_cmdarg('++nobin')
      Assert Equals(ret, { 'nobinary': 1 })
    End

    It parses "++edit" and return "edit: 1" dictionary
      let ret = Buffer.parse_cmdarg('++edit')
      Assert Equals(ret, { 'edit': 1 })
    End

    It parses multiple opts specification and return a correct dictionary
      let ret = Buffer.parse_cmdarg(
            \ '++enc=utf-8 ++ff=dos ++bad=keep ++binary ++nobinary ++edit'
            \)
      Assert Equals(ret, {
            \ 'encoding': 'utf-8',
            \ 'fileformat': 'dos',
            \ 'bad': 'keep',
            \ 'binary': 1,
            \ 'nobinary': 1,
            \ 'edit': 1,
            \})
    End
  End
End
